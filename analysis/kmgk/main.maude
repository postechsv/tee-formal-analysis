load ../../spec/src/main
load consts
load gatekeeper-ta
load gatekeeper-ra

mod KMGK is
  pr KMGK-CONSTANTS .
  pr GATEKEEPER-RA-PROGRAM .
  pr GATEKEEPER-RA-BEHAVIOR .
  pr GATEKEEPER-TA-PROGRAM .
  pr IMP-SEMANTICS .
endm

omod SCENARIO-AUX is
  pr KMGK .
  pr ID-GEN .
  pr RICH-APP .
  pr TRUST-APP .

  var P : Program .
  var RAID : Oid .
  var TAINSTID : TaInstId .
  var APPID : AppId .

------------------------------------------------------------
--- Initialization Functions
------------------------------------------------------------

  op osId : -> Oid .
  op initTrustedOS : TaInstId AppId -> Configuration .
  eq initTrustedOS(TAINSTID, APPID)
  = < osId : TeeOSKernel | opened-session : (sessionId(0) |-> TAINSTID) , session-counter : 1 ,
                           storage : APPID |-> secretID , storage-status : (APPID |-> storageNormal) ,
                           service-request-ta : noAppId , service-request-ta-inst : noInstId ,
                           service-in : nil , service-out : nil ,
                           temporary-objects : empty > .

  op initRa : Oid Program -> Configuration .
  eq initRa(RAID, P) = < RAID : RichApp | proc : none , prog : P , running : false , 
                                          invoke-req-queue : nil , invoke-ret-queue : nil > .

  op initTa : TaInstId Program AppId -> Configuration .
  eq initTa(TAINSTID, P, APPID) = < TAINSTID : TrustApp | proc : none , prog : P , running : false ,
                                                          app-status : normal , api-call : noCall , api-return : noReturn ,
                                                          current-api : noApi , api-state : noState , current-params : empty , id-counter : 0 ,
                                                          trust-app-id : APPID ,
                                                          task : noTask , task-counter : 0 , state-changed : false > .

  ops masterKeyObj masterKeyObjStream : -> TaObjectId [ctor] .
  op masterKey : -> Configuration .
  eq masterKey = 
    < masterKeyObj : PersistentObject | handle-set : none , object-type : TEE-TYPE-DATA , max-object-size : 0 Bits  , 
                                        file-id : secretID , trust-app-id : GATEKEEPER ,
                                        attribute-set : empty , object-usage-set : empty , data-stream-id : masterKeyObjStream >
    < masterKeyObjStream : DataStreamObject | data-size : dataSize(1) , content : newContent(randomData) >
  .
endom

mod BASE-SCENARIO is
  pr DEVICE .
  pr SCENARIO-AUX .

  vars RAPROG TAPROG : Program .
  var RAID : Oid .
  var TAINSTID : TaInstId .
  var APPID : AppId .

  op initDevice : Oid Program TaInstId Program AppId -> Configuration .
  eq initDevice(RAID, RAPROG, TAINSTID, TAPROG, APPID) = 
  < device : Device | execution-envs : {initRa(RAID , RAPROG)} | 
                                       {initTa(TAINSTID , TAPROG , APPID) initTrustedOS(TAINSTID , APPID) masterKey} > 
  .

  op scenario : Oid Program TaInstId Program AppId -> Configuration .
  eq scenario(RAID, RAPROG, TAINSTID, TAPROG, APPID) 
   = initDevice(RAID, RAPROG, TAINSTID, TAPROG, APPID) .

endm

--- red scenario(gkRa, gatekeeperRA, kmTa, gatekeeperTA, GATEKEEPER) .
rew scenario(gkRa, gatekeeperRA, kmTa, gatekeeperTA, GATEKEEPER) .