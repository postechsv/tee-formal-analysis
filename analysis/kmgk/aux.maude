load main

fmod TA-INFO is
  pr ID-GEN .
  pr IMP-PROGRAM .

  sort TaInfo .

  op taInfo : TaInstId Program AppId -> TaInfo [ctor] .
endfm

view TaInfo from TRIV to TA-INFO is
  sort Elt to TaInfo .
endv

omod SCENARIO-AUX is
  pr KMGK .
  pr RICH-APP .
  pr TRUST-APP .
  pr LIST{TaInfo} .

  vars RAID UI : Oid .
  var INSTID : TaInstId .
  var P : Program .
  var APPID : AppId .
  var TAINFO : TaInfo .
  var TAINFOL : List{TaInfo} .
  var UIL : List{UserInput} .

------------------------------------------------------------
--- Initialization Functions
------------------------------------------------------------

  op initStorage : List{TaInfo} -> Map{AppId,Set{FileId}} .
  eq initStorage(taInfo(gkTa, P, GATEKEEPER) TAINFOL) = (GATEKEEPER |-> secretID), initStorage(TAINFOL) .
  eq initStorage(taInfo(kmTa, P, KEYMASTER) TAINFOL) = (KEYMASTER |-> authTokenKeyId), initStorage(TAINFOL) .
  eq initStorage(nil) = empty .
  
  op initStorStatus : List{TaInfo} -> Map{AppId,StorageStatus} .
  eq initStorStatus(taInfo(INSTID, P, APPID) TAINFOL) = (APPID |-> storageNormal), initStorStatus(TAINFOL) .
  eq initStorStatus(nil) = empty .

  op osId : -> Oid .
  op initTrustedOS : List{TaInfo} -> Configuration .
  eq initTrustedOS(TAINFOL)
  = < osId : TeeOSKernel | opened-session : (sessionId(0) |-> gkTa) , session-counter : 1 ,
                           storage : initStorage(TAINFOL) , storage-status : initStorStatus(TAINFOL) ,
                           service-request-ta : noAppId , service-request-ta-inst : noInstId ,
                           service-in : nil , service-out : nil ,
                           temporary-objects : empty > .

  op initRa : Oid Program -> Configuration .
  eq initRa(RAID, P) = < RAID : RichApp | proc : none , prog : P , running : false , 
                                          invoke-req-queue : nil , invoke-ret-queue : nil > .

  op initTa : TaInfo -> Configuration .
  eq initTa(taInfo(INSTID, P, APPID)) = < INSTID : TrustApp | proc : none , prog : P , running : false ,
                                                              app-status : normal , api-call : noCall , api-return : noReturn ,
                                                              current-api : noApi , api-state : noState , current-params : empty , id-counter : 0 ,
                                                              trust-app-id : APPID ,
                                                              task : noTask , task-counter : 0 , state-changed : false > .

  op initTas : List{TaInfo} -> Configuration .
  eq initTas(TAINFO TAINFOL) = initTa(TAINFO) initTas(TAINFOL) .
  eq initTas(nil) = none .

  --- TODO: Update key data (currently, transient object storing key data in TA initiailized with randomAttrVal)
  ops masterKeyObj masterKeyObjStream : -> TaObjectId [ctor] .
  op masterKey : -> Configuration .
  eq masterKey = 
    < masterKeyObj : PersistentObject | handle-set : none , object-type : TEE-TYPE-DATA , max-object-size : 0 Bits  , 
                                        file-id : secretID , trust-app-id : GATEKEEPER ,
                                        attribute-set : empty , object-usage-set : empty , data-stream-id : masterKeyObjStream >
    < masterKeyObjStream : DataStreamObject | data-size : dataSize(1) , content : newContent(randomData) >
  .

  ops authKeyObj authKeyObjStream : -> TaObjectId [ctor] .
  op authKey : -> Configuration .
  eq authKey = 
    < authKeyObj : PersistentObject | handle-set : none , object-type : TEE-TYPE-DATA , max-object-size : 0 Bits  , 
                                      file-id : authTokenKeyId , trust-app-id : KEYMASTER ,
                                      attribute-set : empty , object-usage-set : empty , data-stream-id : authKeyObjStream >
    < authKeyObjStream : DataStreamObject | data-size : dataSize(1) , content : newContent(randomData) >
  .

  op keys : -> Configuration .
  eq keys = masterKey authKey .

  op initUser : Oid List{UserInput} -> Configuration .
  eq initUser(UI, UIL) = 
    < UI : User | authenticator : at , authenticated : false , waiting : false , inputs : UIL >
  .
endom