load ../../spec/src/main
load consts
load user
load authenticator
load gatekeeper-ta
load gatekeeper-ra
load keymaster-ta
load keymaster-ra

mod KMGK is
  pr KMGK-CONSTANTS .
  pr GATEKEEPER-RA-PROGRAM .
  pr GATEKEEPER-RA-BEHAVIOR .
  pr GATEKEEPER-TA-PROGRAM .
  pr KEYMASTER-RA-PROGRAM .
  pr KEYMASTER-RA-BEHAVIOR .
  pr KEYMASTER-TA-PROGRAM .
  pr KMGK-USER-BEHAVIOR .
  pr KMGK-AUTHENTICATOR-BEHAVIOR .
  pr IMP-SEMANTICS .
endm

omod SCENARIO-AUX is
  pr KMGK .
  pr ID-GEN .
  pr RICH-APP .
  pr TRUST-APP .

  var P : Program .
  vars RAID UI : Oid .
  var TAINSTID : TaInstId .
  var APPID : AppId .
  var PWDL : List{Password} .

------------------------------------------------------------
--- Initialization Functions
------------------------------------------------------------

  op osId : -> Oid .
  op initTrustedOS : TaInstId AppId -> Configuration .
  eq initTrustedOS(TAINSTID, APPID)
  = < osId : TeeOSKernel | opened-session : (sessionId(0) |-> TAINSTID) , session-counter : 1 ,
                           storage : APPID |-> secretID , storage-status : (APPID |-> storageNormal) ,
                           service-request-ta : noAppId , service-request-ta-inst : noInstId ,
                           service-in : nil , service-out : nil ,
                           temporary-objects : empty > .

  op initRa : Oid Program -> Configuration .
  eq initRa(RAID, P) = < RAID : RichApp | proc : none , prog : P , running : false , 
                                          invoke-req-queue : nil , invoke-ret-queue : nil > .

  op initTa : TaInstId Program AppId -> Configuration .
  eq initTa(TAINSTID, P, APPID) = < TAINSTID : TrustApp | proc : none , prog : P , running : false ,
                                                          app-status : normal , api-call : noCall , api-return : noReturn ,
                                                          current-api : noApi , api-state : noState , current-params : empty , id-counter : 0 ,
                                                          trust-app-id : APPID ,
                                                          task : noTask , task-counter : 0 , state-changed : false > .

  ops masterKeyObj masterKeyObjStream : -> TaObjectId [ctor] .
  op masterKey : -> Configuration .
  eq masterKey = 
    < masterKeyObj : PersistentObject | handle-set : none , object-type : TEE-TYPE-DATA , max-object-size : 0 Bits  , 
                                        file-id : secretID , trust-app-id : GATEKEEPER ,
                                        attribute-set : empty , object-usage-set : empty , data-stream-id : masterKeyObjStream >
    < masterKeyObjStream : DataStreamObject | data-size : dataSize(1) , content : newContent(randomData) >
  .

  ops authKeyObj authKeyObjStream : -> TaObjectId [ctor] .
  op authKey : -> Configuration .
  eq authKey = 
    < authKeyObj : PersistentObject | handle-set : none , object-type : TEE-TYPE-DATA , max-object-size : 0 Bits  , 
                                      file-id : authTokenKeyId , trust-app-id : KEYMASTER ,
                                      attribute-set : empty , object-usage-set : empty , data-stream-id : authKeyObjStream >
    < authKeyObjStream : DataStreamObject | data-size : dataSize(1) , content : newContent(randomData) >
  .

  op keys : -> Configuration .
  eq keys = masterKey authKey .

  op initUser : Oid List{Password} -> Configuration .
  eq initUser(UI, PWDL) = 
    < UI : User | authenticator : at , authenticated : false , passwords : PWDL , checking : false >
  .
endom

mod BASE-SCENARIO is
  pr DEVICE .
  pr SCENARIO-AUX .

  vars RAPROG TAPROG : Program .
  var RAID : Oid .
  var TAINSTID : TaInstId .
  var APPID : AppId .

  op initDevice : Oid Program TaInstId Program AppId -> Configuration .
  eq initDevice(RAID, RAPROG, TAINSTID, TAPROG, APPID) = 
  < at : Authenticator | execution-envs : {initRa(RAID , RAPROG)} | 
                                          {initTa(TAINSTID , TAPROG , APPID) initTrustedOS(TAINSTID , APPID) keys} ,
                         enrolled-passwords : (1 |-> pwd(randomData)) ,
                         enrolled-handles : (1 |-> pwdHandle(1 , 1 , 1 , 1 , macValue(randomData, TEE-ALG-HMAC-SHA256, key(teeAttribute(TEE-ATTR-SECRET-VALUE, randomAttrVal))) , 1)) > 
  .

  op scenario : Oid Program TaInstId Program AppId -> Configuration .
  eq scenario(RAID, RAPROG, TAINSTID, TAPROG, APPID) 
   = initDevice(RAID, RAPROG, TAINSTID, TAPROG, APPID) initUser(ui(1), (pwd(randomData) pwd(randomData))).
  ---  = initDevice(RAID, RAPROG, TAINSTID, TAPROG, APPID) initUser(ui(1), (pwd(noData) pwd(noData))) .

endm

--- red scenario(gkRa, gatekeeperRA, gkTa, gatekeeperTA, GATEKEEPER) .
rew scenario(gkRa, gatekeeperRA, gkTa, gatekeeperTA, GATEKEEPER) .